plugins {
	id 'java'
	id 'application'
	id "com.jfrog.bintray" version "1.8.2"
	id 'maven'
	id 'maven-publish'
	id 'idea'
	id "org.javamodularity.moduleplugin" version "1.3.0"
}

sourceCompatibility = 11
targetCompatibility = 11

def projectGroup = 'me.joshlarson'
def projectName = 'jlcommon-fx'
def projectVersion = '1.0.3'
group = projectGroup
version = projectVersion

repositories {
	jcenter()
}

dependencies {
	compile group: "me.joshlarson", name: "jlcommon", version: "1.8.0"
	compileOnly "org.openjfx:javafx-base:11.0.1:linux"
	compileOnly "org.openjfx:javafx-graphics:11.0.1:linux"
	compileOnly "org.openjfx:javafx-controls:11.0.1:linux"
	compileOnly "org.openjfx:javafx-fxml:11.0.1:linux"
	
	testCompile 'junit:junit:4.12'
	testCompile "org.openjfx:javafx-base:11.0.1:linux"
	testCompile "org.openjfx:javafx-graphics:11.0.1:linux"
	testCompile "org.openjfx:javafx-controls:11.0.1:linux"
	testCompile "org.openjfx:javafx-fxml:11.0.1:linux"
}

jar {
	baseName = projectName
	classifier = null
	version = projectVersion
	from sourceSets.main.allSource
}

task sourcesJar(type: Jar, dependsOn: classes) {
	baseName = projectName
	classifier = "sources"
	version = projectVersion
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	baseName = projectName
	classifier = "javadoc"
	version = projectVersion
	from javadoc.destinationDir
}

artifacts {
	archives sourcesJar
	archives javadocJar
}

def pomConfig = {
	licenses {
		license {
			name "The MIT License"
			url "https://opensource.org/licenses/MIT"
			distribution "repo"
		}
	}
	developers {
		developer {
			id "josh-larson"
			name "Josh Larson"
			email "joshua-github@ourclan.net"
		}
	}
	
	scm {
		url "https://github.com/Josh-Larson/jlcommon-fx"
	}
}


// Create the publication with the pom configuration:
publishing {
	publications {
		jlcommonfx(MavenPublication) {
			from components.java
			artifact sourcesJar
			artifact javadocJar
			groupId projectGroup
			artifactId projectName
			version projectVersion
			pom.withXml {
				def root = asNode()
				root.appendNode('description', 'Common JavaFX Library')
				root.appendNode('name', 'Common JavaFX Library')
				root.appendNode('url', 'https://github.com/Josh-Larson/jlcommon-fx')
				root.children().last() + pomConfig
			}
		}
	}
}

bintray {
	user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
	key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
	publications = ['jlcommonfx']
	pkg {
		repo = 'jlcommon'
		name = projectName
		licenses = ['MIT']
		vcsUrl = 'https://github.com/Josh-Larson/jlcommon-fx'
		version {
			name = projectVersion
			desc = 'Common JavaFX Library'
			released = new Date()
			vcsTag = 'v' + projectVersion
		}
	}
}
